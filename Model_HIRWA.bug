model{
  
  ## Dimension parameters
  # N = number of patients
  # n[i] = number of observations patient i, i = 1, ..., N
  # n_cum = cumsum(n) (add 0 in the beginning) Dimension N+1
  # p = number of covariates with fixed effects (intercept included)
  # q = number of covariates with random effects (intercept included)
  # alpha
  ### Likelihood
  for(i in 1:N){
    Zi_bi[i] = b[i]* mean_OPP[i];
    
    for(j in ((n_cum[i]+1) : n_cum[i+1])){
      mu[j] <- beta[1] + beta[2]*Sex[j] +  beta[3]*yearofglaucoma[j] + beta[4]*family_history[j] + 
        beta[5]*IOP[j] + beta[6]*Diabetes[j] + beta[7]*MD[j]+ beta[8]*age[j]+
        Zi_bi[i];
      
      Y[j] ~ dnorm(mu[j], tau) ; 
    }
    
  }
  
  ### Constructive DP
  pp[1] <- r[1];
  for (j in 2:M) {
    pp[j] <- r[j] * (1 - r[j - 1]) * pp[j -1 ] / r[j - 1]; # weights 
  }
  p.sum <- sum(pp[]);	
  
  for (j in 1:M){ 
    theta[j] ~ dnorm(mu.b,sigmaInv);   # locations 
    r[j] ~ dbeta(1, alpha);		
    # scaling to ensure sum to 1 
    pi[j] <- pp[j] / p.sum ;		
  }
  
  mu.b ~ dnorm(0,0.01) ;    # mean of the baseline P_0
  sigmaInv ~ dgamma(2.01, 1.01)
  
          
  
  for(i in 1:N){    
    S[i] ~ dcat(pi[]);         # sample 
    b[i] <- theta[S[i]];  
  } 
  
  ### Priors
  beta  ~ dmnorm(mu0, SigmaInv0);
  tau ~ dgamma(alpha0, lambda0);

}